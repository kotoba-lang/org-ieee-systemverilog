(ns systemverilog.interface-test
  (:require [clojure.test :refer [deftest is testing]]
            [systemverilog.datatype :as datatype]
            [systemverilog.interface :as iface]))

(def ^:private bus-signals
  [(datatype/signal :clk :logic 1)
   (datatype/signal :data :logic 8)
   (datatype/signal :valid :bit 1)])

(deftest well-formed-interface-has-no-dangling-modport-refs
  (let [bus (iface/make-interface :bus_if bus-signals
                                   {:master {:clk :input :data :output :valid :output}
                                    :slave {:clk :input :data :input :valid :input}})]
    (is (empty? (iface/dangling-refs bus)))
    (is (iface/valid? bus))
    (is (= #{:clk :data :valid} (iface/signal-names bus)))))

(deftest dangling-modport-ref-is-reported
  (testing "a modport referencing a signal absent from the interface is flagged"
    (let [bus (iface/make-interface :bus_if bus-signals
                                     {:master {:clk :input :ready :input}})]
      (is (= [[:master :ready]] (iface/dangling-refs bus)))
      (is (not (iface/valid? bus)))))
  (testing "an interface with no modports at all is trivially valid"
    (is (iface/valid? (iface/make-interface :bus_if bus-signals nil)))))
